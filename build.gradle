buildscript {
	repositories {
		jcenter()
	}
	dependencies {
		//used for dependencyLicenseReport
		classpath "com.smokejumperit.gradle.license:Gradle-License-Report:0.0.2"
	}
}

plugins {
  id "java"
  id "maven-publish"
  id "eclipse"
  id "idea"
  id "com.jfrog.bintray" version "1.5"
}

// gradle dependencyLicenseReport to generate your report in build/reports/dependency-license
apply plugin:'license-report'

sourceCompatibility = "1.7"
targetCompatibility = "1.7"

repositories {
  jcenter()
	maven { url "http://developer.marklogic.com/maven2/" }
}

dependencies {
  compile 'com.marklogic:java-client-api:4.0.0-EA4'
  compile 'com.marklogic:marklogic-xcc:9.0-EA4'
  compile 'org.jdom:jdom2:2.0.6'
  compile 'org.springframework:spring-context:4.3.7.RELEASE'

  testCompile 'junit:junit:4+'
  testCompile 'org.springframework:spring-test:4.3.7.RELEASE'

  // Used for testing loading modules from the classpath
  testRuntime files("lib/modules.jar")
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier 'sources'
  from sourceSets.main.allJava
}

javadoc.destinationDir = file(javadocsDir + "/" + project.name)
javadoc.failOnError = false

publishing {
  publications {
    mainJava(MavenPublication) {
      from components.java
    }
    sourcesJava(MavenPublication) {
      from components.java
      artifact sourcesJar
    }
  }
}

if (project.hasProperty("myBintrayUser")) {
  bintray {
    user = myBintrayUser
    key = myBintrayKey
    publications = ['mainJava', 'sourcesJava']
    pkg {
      repo = 'maven'
      name = project.name
      licenses = ['Apache-2.0']
      vcsUrl = 'https://github.com/rjrudin/' + project.name + '.git'
      version {
        name = project.version
        released = new Date()
      }
    }
  }
}
